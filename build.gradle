plugins {
  id "me.champeau.gradle.jmh" version "0.4.2"
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'idea'

    group = 'me.champeau.sample'
    version = '1.0-SNAPSHOT'

    sourceCompatibility = 1.7
    targetCompatibility = 1.7

    repositories {
        jcenter()
    }
}


apply plugin: 'me.champeau.gradle.jmh'

jmh {
    include = 'org\\.openjdk\\.jmh\\.samples\\.JMHSample_01_HelloWorld.*'
    humanOutputFile = null
    warmupIterations = 1
    iterations = 1
}

// it is possible to tweak the jmhJar task directly
jmhJar.baseName = 'custom'

def lastJmh = tasks.jmh

subprojects {
    apply plugin: 'me.champeau.gradle.jmh'

    jmh {
        include = 'org\\.openjdk\\.jmh\\.samples\\.JMHSample_01_HelloWorld.*'
        humanOutputFile = null
        warmupIterations = 1
        iterations = 1
    }

    tasks.jmh {
        mustRunAfter lastJmh
    }

    lastJmh = tasks.jmh
}
